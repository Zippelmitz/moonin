#!/usr/bin/perl
#
#

use strict;
use warnings;

use FindBin;
use lib ("$FindBin::Bin/../lib");
use Schedule::Cron;
use Moonin::Config;
use Moonin::Node;
use Moonin::Graph;
use Log::Log4perl qw(:levels);

my $conf = q(
log4perl.rootLogger=INFO, Screen
log4perl.appender.Screen=Log::Log4perl::Appender::Screen
log4perl.appender.Screen.layout=Log::Log4perl::Layout::PatternLayout
log4perl.appender.Screen.stderr=0
log4perl.appender.Screen.layout.ConversionPattern=[%d] [%C (%L)] [%p] %m%n
            );
Log::Log4perl::init( \$conf );

my $config =
  Moonin::Config->new( config_file => "$FindBin::Bin/../conf/moonin.conf" );

my $cron = Schedule::Cron->new(
  \&dispatch,
  log           => \&log_method,
  processprefix => "Moonin"
);
foreach my $domain_name ( keys( %{ $config->domain } ) ) {
  foreach
    my $node_name ( keys( %{ $config->domain->{$domain_name}->{node} } ) ) {
    $cron->add_entry( "*/5 * * * *", [ $domain_name, $node_name ] );
  }
}

my $big_logger = Log::Log4perl->get_logger("Moonin");
$cron->add_entry( "*/8 * * * *", \&graph );
$big_logger->info("Starting Moonin Poller");
$cron->run;

exit 0;

sub log_method {
  my ( $level, $msg ) = @_;
  my $DBG_MAP = { 0 => $INFO, 1 => $WARN, 2 => $ERROR };

  my $logger = Log::Log4perl->get_logger("Moonin");
  $logger->log( $DBG_MAP->{$level}, $msg );
}

# First, poll the nodes
sub dispatch {
  my ( $domain_name, $node_name, $type ) = @_;

  my $node = Moonin::Node->new(
    domain => $domain_name,
    name   => $node_name,
    config => $config,
  );

  my $logger = Log::Log4perl->get_logger("Moonin");
  $logger->info("Fetching node $domain_name $node_name");
  $node->process;
}

# Optionally, graph the results
sub graph {
  my $logger = Log::Log4perl->get_logger("Moonin");
  foreach my $domain ( keys( %{ $config->domain } ) ) {
    foreach my $node ( keys( %{ $config->domain->{$domain}->{node} } ) ) {
      $logger->info("Pre-Processing graphs for $domain $node");
      my $node = Moonin::Node->new(
        domain => $domain,
        name   => $node,
        config => $config,
      );
      my $graph = Moonin::Graph->new( node => $node );
      $graph->pre_process_all;
    }
  }
}

1;
